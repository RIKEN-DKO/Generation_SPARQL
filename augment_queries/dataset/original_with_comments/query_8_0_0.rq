# What are the anatomical entities where the human gene APOC1 is expressed in the post-juvenile stage along with its expression score independently of the strain, sex, and cell type?
PREFIX orth: <http://purl.org/net/orth#>
PREFIX genex: <http://purl.org/genex#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX up-taxon:<http://purl.uniprot.org/taxonomy/>
PREFIX ensembl: <http://rdf.ebi.ac.uk/resource/ensembl/>
PREFIX lscr: <http://purl.org/lscr#>
SELECT DISTINCT ?anatName ?expression ?score ?condition ?seq ?strain ?anat WHERE { 
	?seq a orth:Gene .
	?expression a genex:Expression .
	?expression genex:hasExpressionCondition ?condition . # has an expression condition
	?expression genex:hasExpressionLevel ?score .
	?expression genex:hasSequenceUnit ?seq . # has sequence unit
	?seq lscr:xrefEnsemblGene ensembl:ENSG00000130208 .
	?condition a genex:ExpressionCondition.
	?condition genex:hasAnatomicalEntity ?anat . # has anatomical entity
	?anat a genex:AnatomicalEntity.
	?anat rdfs:label ?anatName .
	?condition genex:hasDevelopmentalStage obo:UBERON_0000113 . # has a developmental stage
	?condition genex:hasSex "any".
	?condition genex:hasStrain ?strain . # has strain
	?strain a efo:EFO_0005135.
	?strain rdfs:label "wild-type" .
	FILTER( ?anat !=  obo:GO_0005575 )
}