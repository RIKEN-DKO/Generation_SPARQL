# What are the anatomical entities including cell types, if any, where the human gene APOC1 is expressed at the post-juvenile stage along with its expression score independently of the strain and sex? Provide also the taxon scientific name and the taxon common name and the anatomical entity description.
PREFIX orth: <http://purl.org/net/orth#>
PREFIX genex: <http://purl.org/genex#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX up: <http://purl.uniprot.org/core/>
SELECT DISTINCT ?cellType ?expressioncondition ?organism ?taxon ?anatomicalentity_description ?anatomicalentity ?anatomicalentityName ?cellTypeName ?expression ?score ?efo_0005135 ?taxon_scientificname ?stage ?gene ?taxon_commonname WHERE { 
	?gene a orth:Gene .
	?expression a genex:Expression .
	?expression genex:hasExpressionCondition ?expressioncondition .
	?expression genex:hasExpressionLevel ?score .
	?expression genex:hasSequenceUnit ?gene .
	?gene rdfs:label "APOC1" .
	?expressioncondition a genex:ExpressionCondition.
	?expressioncondition genex:hasAnatomicalEntity ?anatomicalentity .
	?anatomicalentity a genex:AnatomicalEntity.
	?anatomicalentity rdfs:label ?anatomicalentityName .
	?expressioncondition genex:hasAnatomicalEntity ?cellType .
	?cellType rdfs:label ?cellTypeName .
	?expressioncondition genex:hasDevelopmentalStage ?stage .
	?stage rdfs:label "post-juvenile" .
	?expressioncondition genex:hasSex "any".
	?expressioncondition genex:hasStrain ?efo_0005135 .
	?efo_0005135 a efo:EFO_0005135.
	?efo_0005135 rdfs:label "wild-type" .
	?gene orth:organism ?organism .
	?organism obo:RO_0002162  ?taxon .
	?taxon a up:Taxon .
	?taxon up:commonName "human" .
	?taxon <http://purl.uniprot.org/core/scientificName> ?taxon_scientificname.
	?taxon <http://purl.uniprot.org/core/commonName> ?taxon_commonname.
	?anatomicalentity <http://purl.org/dc/terms/description> ?anatomicalentity_description.
	FILTER(       ?anatomicalentity != obo:GO_0005575       )
	FILTER(       ?anatomicalentity != ?cellType       )
}